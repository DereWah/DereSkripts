#Handcuffs 1.2 script by DereWah -> https://www.spigotmc.org/members/derewah.240784/
#Language: EN
#If you find any bugs, please report them: TG: https://t.me/skriptitaly

#COMMANDS:
#/handcuff | handcuff.receive | Get handcuffs.
#/handcuff <player> | handcuff.receive | Get keys to unlock a specific player.
#/handcuffdebug | handcuff.debug | Reset the script and variables.
#Currently, the script is not compatible with mask plugins (that hide your identity).
#In the future, I plan to create one, so when that script is released, this script will be updated to be compatible.


Options:
	p: &7[&bPOLICE&7] #Script prefix.
	handcuff_item: gold horse armor #Item representing handcuffs.
	handcuff_name: &b&lHANDCUFFS #Name of the handcuffs item.
	handcuff_lore: &7Click on a player to handcuff them. #Lore of the handcuffs item.
	handcuff_title: &bHANDCUFFED! #Title displayed to a player when they are handcuffed.
	handcuff_subtitle: &7You have been handcuffed. #Subtitle displayed to a player when they are handcuffed.
	key_item: tripwire hook #Item representing handcuff keys.
	key_name: &eHandcuff Keys #Name of the handcuff keys item.
	credits: true #Enable credits for the developer.

command /handcuff [<offlineplayer>]:
	aliases: /manette, /handcuffs
	permission: handcuff.receive
	trigger:
		if arg is not set:
			give handcuffInstance() to player
			send message "{@p} &7You received {@handcuff_name}"
		else:
			give handcuffKeyInstance(arg) to player
			send message "{@p} &7You received %arg%'s {@key_name}&7."
	
command /handcuffdebug:
	permission: handcuff.debug
	trigger:
		clear {hc::*}
		send message "{@p} &aSkript variables reset."

	
on rightclick on player:
	if player's tool is handcuffInstance():
		if {hc::blocked::*} does not contain event-entity:
			handcuffPlayer(event-entity, player)
		else:
			send message "{@p} &cThis player is already handcuffed."
	else if player's tool is handcuffKeyInstance(event-entity):
		if {hc::blocked::*} contains event-entity:
			handcuffRemove(event-entity, player)
				
				
on right click:
	if isHandcuffed(player) is true:
		cancel event
		
		
		
on quit:
	if isHandcuffed(player) is true:
		kill player
		remove player from {hc::blocked::*} #kill on join
		
on join:
	if {hc::blocked::*} contains player:
		kill player
		remove player from {hc::blocked::*} #we check this aswell in case the server crashed.
		
		
		
on damage of player:
	if {hc::blocked::*} contains victim:
		increase the damage by 50

on death of player:
	if {hc::blocked::*} contains victim:
		set {_l} to victim's location
		drop 1 of handcuffInstance() 1 meter above {_l}
		remove victim from {hc::blocked::*}
		if attacker's inventory contains handcuffKeyInstance(victim):
			remove handcuffKeyInstance(victim) from attacker's inventory
			
			
function handcuffRemove(v: player, a: player):
	remove slowness from {_v}'s active potion effects
	remove handcuffKeyInstance({_v}) from {_a}'s inventory
	give handcuffInstance() to {_a}
	soundRemoveHandcuff({_v})
	remove {_v} from {hc::blocked::*}
	if {_a} is set:
		send message "{@p} &b%{_a}%&7 is removing &b%{_v}%&7's handcuff" to {_v} and {_a}	
	
function isHandcuffed(p: offlineplayer) :: boolean:
	if {hc::blocked::*} contains {_p}:
		return true
	else:
		return false
	
function handcuffInstance() :: item:
	set {_hc} to {@handcuff_item}
	set {_hc}'s name to "{@handcuff_name}"
	set 1st line of {_hc}'s lore to "{@handcuff_lore}"	
	return {_hc}
	
	
function handcuffKeyInstance(v: offlineplayer) :: item:
	set {_key} to {@key_item}
	set {_key}'s name to "{@key_name}"
	set 1st line of {_key}'s lore to "&7%{_v}%"
	return {_key}
	
			
function handcuffPlayer(v: player, a: player):
	send message "{@p} &b%{_a}%&7 is handcuffing &b%{_v}%" to {_v} and {_a}
	send title "{@handcuff_title}" with subtitle "{@handcuff_subtitle}" to {_v} for 5 seconds
	apply potion of slowness of tier 20 without particles to {_v} for 1 year
	make {_v} dismount from vehicle
	remove 1 of handcuffInstance() from {_a}'s inventory
	soundHandcuff({_v})
	give handcuffKeyInstance({_v}) to {_a}
	add {_v} to {hc::blocked::*}
	
	
	
function soundHandcuff(p: player):
	set {_l} to location of {_p}
	play sound "minecraft:block.note_block.hat" at volume 1 at pitch 1 at {_l}
	wait 5 tick
	play sound "minecraft:block.note_block.hat" at volume 1 at pitch 1.2 at {_l}
	
	
function soundRemoveHandcuff(p: player):
	set {_l} to location of {_p}
	play sound "minecraft:block.note_block.hat" at volume 1 at pitch 1.2 at {_l}
	wait 5 tick
	play sound "minecraft:block.note_block.hat" at volume 1 at pitch 1 at {_l}	
	
on join:
	wait 1 second
	if {@credits} is true:
		send message "{@p}&b Plugin by &9<link:https://www.spigotmc.org/members/derewah.240784/>DereWah &b[&9&nLINK&b]<reset>"	
		
		
on mount:
	if isHandcuffed(player) is true:
		cancel event
		
on jump:
	if isHandcuffed(player) is true:
		cancel event	
		
on before crackshot shoot:
	if isHandcuffed(player) is true:
		cancel event	
