#DereGPS Skript 1.0 by DereWah -> https://t.me/SkriptItaly
#Language: Traditional Chinese (Thanks to fatmeow404 for the translation)

#Fiverr: https://www.fiverr.com/derewah/create-a-professional-minecraft-plugin-with-skript-for-your-server

#-----------------REQUIREMENTS-----------------
#Skript 2.6 (https://github.com/SkriptLang/Skript/releases)
#SkBee 2.9.0 (https://github.com/ShaneBeee/SkBee/releases)
#----------------------------------------------

#-----------------COMMANDS-----------------
#/deregps stop | deregps.use | stops the GPS.
#/deregps create | deregps.admin | create a location for the GPS.
#/deregps delete | deregps.admin | remove a location for the GPS.
#/deregps list | deregps.use | get a list of all the locations. If you have the permission deregps.admin, you will also see the button to remove the GPS location.
#/deregps <location> | deregps.use | starts tracking a location
#----------------------------------------------

Options:
	p: &7[&a德瑞的&2定位系統&7]
	gps_forward:  #E170
	gps_forwardleft:  #E171
	gps_left:  #E172
	gps_backwardsleft:   #E173
	gps_backwards:  #E174
	gps_backwardsright:  #E175
	gps_right:  #E176
	gps_forwardright:  #E177
	gps_icon:  #E178
	gps_stop_radius: 3
	credits: true #developer credits
	
	

command /deregps [<text>] [<text>]:
	aliases: /gps, /dgps
	permission: deregps.use
	permission message: {@p} &c你沒有權限 &4deregps.use&c.
	trigger:
		if arg-1 is "stop":
			if isDereGPSTracking(player) is true:
				clear {deregps::players::%player%::*}
				send message "{@p} &aGPS 以解除."
			else:
				send message "{@p} &c你沒有使用gps."
		else if arg-1 is "create" or "delete":
			if player has permission "deregps.admin":
				if arg-1 is "create":
					if arg-2 is set:
						if indexes of {deregps::locations::*} does not contain lowercase arg-2:
							set {deregps::locations::%lowercase arg-2%} to player's location
							send message "{@p} &a定位點 %lowercase arg-2% 創建成功!"
						else:
							send message "{@p} &c該位置已存在."
					else:
						send message "{@p} &cSpecify the name of the location to create."
				else if arg-1 is "delete":
					if arg-2 is set:
						if indexes of {deregps::locations::*} contains lowercase arg-2:
							clear {deregps::locations::%lowercase arg-2%}
							send message "{@p} &a你刪除了定位點 %lowercase arg-2%"
						else:
							send message "{@p} &c該位置不存在."
					else:
						send message "{@p} &c指定要刪除的位置的名稱."
			else:
				send message "{@p} &c你沒有權限 &4deregps.admin&c."
		else if arg-1 is "list":
			if player does not have permission "deregps.admin":
				loop indexes of {deregps::locations::*}:
					send message formatted "<cmd:/gps %loop-value%>&7[&r&7] &a- &2%loop-value%"
			else:
				loop indexes of {deregps::locations::*}:
					send message formatted "<cmd:/gps delete %loop-value%>&7[&c&lX&7]<reset> <cmd:/gps %loop-value%>&7[&f{@gps_icon}&7]<reset> &a- &2%loop-value%"				
		else:
			if isDereGPSTracking(player) is false:
				if arg-1 is set:
					if indexes of {deregps::locations::*} contains lowercase arg-1:
						set {deregps::players::%player%::target} to  {deregps::locations::%lowercase arg-1%}
						startDereGPS(player)
					else:
						send message "{@p} &c該位置不存在。"
				else:
					make player execute "/gps list"
			else:
				send message "{@p} &cYour GPS is already running."
	
on join:
	wait 1 second
	if {@credits} is true:
		send message formatted "{@p}&b 腳本作著: &9<link:https://www.fiverr.com/derewah/create-a-professional-minecraft-plugin-with-skript-for-your-server>DereWah &b[&9&n連結&b]<reset>"
	
on load:
	clear {deregps::icons::*}
	set {deregps::icons::1} to "{@gps_forwardright}" #177 /^
	set {deregps::icons::2} to "{@gps_forward}" #170 |^
	set {deregps::icons::3} to "{@gps_forwardleft}" #171 ^\
	set {deregps::icons::4} to "{@gps_left}" #172 <-
	set {deregps::icons::5} to "{@gps_backwardsleft}" #173 -/
	set {deregps::icons::6} to "{@gps_backwards}" #174
	set {deregps::icons::7} to "{@gps_backwardsright}" #175
	set {deregps::icons::8} to "{@gps_right}" #176 ->

		
function isDereGPSTracking(p: offlineplayer) :: boolean:
	if {deregps::players::%{_p}%::target} is set:
		return true
	return false
	
on quit:
	clear {deregps::players::%player%::target}

function startDereGPS(p: offlineplayer):
	set {_b} to bossbar named "deregps.%{_p}%" with title "" with style solid
	add {_p} to bossbar players of {_b}
	send message "{@p} &aGPS 已啟用. 使用 &c/gps stop&a 來停止追蹤定位點." to {_p}
	while {deregps::players::%{_p}%::target} is set:
		set {_d} to ceil(distance between {_p}'s location and {deregps::players::%{_p}%::target})
		
		
		set {_dir} to vector from yaw {_p}'s yaw and pitch 0
		set {_diff} to normalized vector between {_p}'s location and {deregps::players::%{_p}%::target}
		
		set {_cross} to ({_diff} cross {_dir}) dot vector(0,1,0)
		set {_dot} to {_diff} dot {_dir}
		
		set {_angle} to atan2({_cross}, {_dot})
		
		set {_index} to floor(({_angle}+22 )/ 45)
		if {_index} is -4:
			set {_index} to 4
		else if {_index} <= 0:
			add 8 to {_index}	
		set bossbar title of {_b} to "%{deregps::icons::%{_index}%}% &7[&e%{_d}% 格&7]"
		if {_d} <= {@gps_stop_radius}:
			clear {deregps::players::%{_p}%::target}
			send message "{@p} &a您已到達目的地." to {_p}
		wait 2 ticks
	delete bossbar named "deregps.%{_p}%"
