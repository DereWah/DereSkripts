Options:
	p: &7[&dXP&bC&7]
	
Variables:
	{xpc::tax} = 0.3
#The amount of levels, in percentage, taken from the player as a tax.
#If a player has 100 xp and the tax is set to 0.2 he will obtain bottles 80 levels worth of xp.
#Keep this value between 0 and 1.

	{xpc::minimum} = 7
#Minimum amount of levels required to convert them into bottles.
#I suggest keeping this higher than 6, since xp bottles orb drops are random and with low levels players can end up creating infinite xp bottles.



command /xpconvert:
	permission: derewah.xpconvert
	cooldown: 10 minutes
	cooldown message: {@p} &cWait %remaining time% before using again this command.
	trigger:
		if level of player >= {xpc::minimum}:
			set {_level} to level of player
			set level of player to 0
			set level progress of player to 0
			if {_level} < 16:
				set {_xp} to {_level}*{_level}+6*{_level}
			else if {_level} < 31:
				set {_xp} to 2.5*{_level}*{_level}-40.5*{_level}+360
			else:
				set {_xp} to 4.5*{_level}*{_level}-162.5*{_level}+2220
			set {_xp} to {_xp}*(1 - {xpc::tax})
			set {_bottles} to floor({_xp} / 6)
			send message "{@p} &5%{_level}%&d levels of &5XP&d have been converted to &5%{_bottles}%&d xp bottles."
			give {_bottles} of experience bottles to player
		else:
			send message "{@p} &cYou need atleast %{xpc::minimum}% levels of xp to convert them into bottles."